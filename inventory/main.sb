'aesthetic purposes
TextWindow.BackgroundColor = "blue"
TextWindow.ForegroundColor = "white"
TextWindow.Clear()

'meta
br = Text.GetCharacter(10) 'line break
stockLabel = "1=Name;2=Code;3=Qty;4=Price"
orderLabel = "1=Date;2=Code;3=Qty"

Sub break
  TextWindow.Write(br)
EndSub

'low level split function
'usage: input "s", "d"; output "ls"
s = "" 'input string
d = "" 'delimeter
Sub split
  ls = "" 'init output
  i = 1 'init index
  t = s 'copy input
  p = Text.GetIndexOf(t, d)
  While (p > 0) 'while "d" in "t"
    ls[i] = Text.GetSubText(t, 1, p-1)
    i = i + 1
    t = Text.GetSubTextToEnd(t, p+1) 'cut t
    p = Text.GetIndexOf(t, d)
  EndWhile
  ls[i] = t 'last item
EndSub
'cite: https://social.msdn.microsoft.com/Forums/en-US/6c4894a8-2a1f-4ce7-94a9-6ec6dc74db78/smallbasic-split-function

'pseudo-csv string -> list function
'usage: input "s"; output "ls"
s = "" 'input string
Sub listc
  d = br
  split()
  cs = ls
  For j=1 to Array.GetItemCount(cs)
    s = cs[j]
    d = ","
    split()
    cs[j] = ls
  EndFor
EndSub

'pseudo-csv file read function
'usage: input "n"; output "ls"
name = "" 'target file name
Sub readc
  s = File.ReadContents(Program.Directory+"/"+name)
  listc()
EndSub

'item swap low level function
'usage: input "ls", "i", "j"; output "ls"
ls = "" 'input list
i = 0
j = 0
Sub swap
  _ = ls[i]
  ls[i] = ls[j]
  ls[j] = _
EndSub

'string bubble sort *by column*
'usage: input "ls", "c"; output "ls"
col = 0 'column
Sub sortStr
  k = Array.GetItemCount(ls) 'array size
  For i=1 To k-1 'kÂ² complexity
    x = ls[i][col]
    For j=i+1 To k
      y = ls[j][col]
      If (x <> y) Then 'duplicate exception
        c = 0
        cx = 0
        cy = 0
        While (cx = cy)
          c = c + 1
          cx = Text.GetCharacterCode(Text.GetSubText(x,c,1))
          cy = Text.GetCharacterCode(Text.GetSubText(y,c,1))
        EndWhile
        If (cx > cy) Then ' "<" for descending
          swap()
        EndIf
      EndIf
    EndFor
  EndFor
EndSub

'Sub sortStr but int
'usage: input "ls", "c"; output "ls"
Sub sortInt
  k = Array.GetItemCount(ls)
  For i=1 To k-1 'array size
    x = ls[i][col]
    For j=i+1 To k
      y = ls[j][col]
      If (x > y) Then ' "<" for descending
        swap()
      EndIf
    EndFor
  EndFor
EndSub

'matrix dataset listing function
'usage: input "title", "label", "ms"; output console
title = ""
label = ""
ms = "" 'matrix
Sub list2D
  TextWindow.WriteLine(title)
  For i=1 To Array.GetItemCount(ms)
    For j=1 To Array.GetItemCount(ms[i])
      TextWindow.WriteLine(label[j]+": "+ms[i][j])
    EndFor
    break()
  EndFor
EndSub

'usage: input "stock", "order"; output "leftover"
stock = ""
order = ""
Sub stockLeft 'calculate stock left after order
  leftover = stock
  For i=1 To Array.GetItemCount(order)
    code = order[i][2]
    k = Array.GetItemCount(leftover)
    For j=1 To k
      If code = leftover[j][2] Then
        leftover[j][3] = leftover[j][3] - order[i][3]
        j = k 'break loop, blame sb
      EndIf
    EndFor
  EndFor
  title = "Leftover" 
  label = "1=Name;2=Code;3=Qty;4=Price"
  ms = leftover 
  list2D()
EndSub

'check critical stock
'assumption: neither "leftover" or "stock" is sorted again after stockLeft()
leftover = ""
Sub critStock
  TextWindow.WriteLine("Stock Checking...")
  For i=1 To Array.GetItemCount(stock)
    code = stock[i][2]
    lStock = leftover[i][3]
    cStock = Math.Round(stock[i][3]/3)
    If lStock = 0 Then
      TextWindow.WriteLine(stock[i][1]+" stockout")
    ElseIf lStock < 0 Then 'logically impossible, but whatever
      TextWindow.WriteLine(stock[i][1]+" quantum stockout")
    ElseIf lStock < cStock Then
      TextWindow.WriteLine(stock[i][1]+" stockout soon")
    EndIf
  EndFor
  break()
EndSub
  
name = "stock.txt"
readc()
stock = cs

name = "order.txt"
readc()
order = cs

'sort stock by code
ls = stock
col = 2
sortStr()
stock = ls

'sort order by date
ls = order
col = 1
sortStr()
order = ls

stockLeft()
critStock()